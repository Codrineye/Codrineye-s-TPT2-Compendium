:import combiner lib
:name {name}
:budget_cap max

; Impulses

wakeup()

key.{inv_fill}()

key.{start}()
key.{gift_up}()
key.{gift_down}()

key.{up}()
key.{down}()
key.{left}()
key.{right}()

not(contains({combiner.get}, "combining"))

:local double target_gift
:local double inventory

; Using arithmetic.double to determine the sign of the operation.
; default value for target_gift is 2.0, so we always start on blue gift
target_gift = min(2.0, max(0.0,\
  arithmetic.double(\
    s2d(sub({combiner.get}, 0, 1), 2.0),\
    if(impulse() == "key.{gift_up}", "+", "-"),\
    if(contains("key.{gift_up}|key.{gift_down}", impulse()), 1.0, 0.0)\
  )\
))

; Using arithmetic.double to determine the sign of the operation
; Subtraction happens if we're going up or to the left, otherwise it's addition
; We subtract by 0 if we're called with anything other than {up}{down}{left}{right}
; up/down mean change row, so shift by 12. left/right means change collumn, so shift by 1
; default value for inventory is 36 because that's the value I chose when making this script
inventory = min(47.0, max(0.0,\
  arithmetic.double(\
    s2d(sub({combiner.get}, 1, 2), 36.0),\
    if(contains("key.{up}|key.{left}", impulse()), "-", "+"),\
    if(\
      contains("wakeup|key.{gift_up}|key.{gift_down}|key.{start}|key.{inv_fill}", impulse()),\
      0.0,\
      if(contains("key.{up}|key.{down}", impulse()), 12.0, 1.0)\
    )\
  )\
))

gotoif(end, not(contains("key.{start}|key.{inv_fill}|{name}", impulse())))

:local int counter

:local vector buyer
:local vector inv_slot

buyer = {pos.relative(\
  (gift_slot_1 + gift_slot_increment * target_gift) / 800.0,\
  gift_coord_y / 450.0,\
  1.0, 0.5\
)}

start:
inv_slot = {pos.relative(\
  (inv_coord_x + inv_slot_offset * (inventory % 12.0)) / 800.0,\
  (inv_coord_y - inv_slot_offset * floor(inventory / 12.0)) / 450.0,\
  1.0, 0.5\
)}

combine_loop:

; buy our selected gift
click(buyer)
; The gift is now in our inventory slot.
;
; Click on the inventory slot to grab our gift
click(inv_slot)
;
; and put our gift in the first combine slot.
{click.relative(combine_slot_1 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; Buy again
click(buyer)
; The gift is now in our inventory slot.
;
; Click on the inventory slot to grab our gift
click(inv_slot)
;
; and put our gift in the second combine slot.
{click.relative(combine_slot_2 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
; And again
click(buyer)
; The gift is now in our inventory slot.
;
; Click on the inventory slot to grab our gift
click(inv_slot)
;
; and put our gift in the second combine slot.
{click.relative(combine_slot_3 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; Now click the Combine button
{click.relative(combine_button / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; Take the combine output from the combe slot
{click.relative(combine_slot_result / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}

counter = (counter + 1) % 3

; We don't sell if this is our third combine
gotoif(no_sell, counter == 0)

; And sell it by putting it in the sell slot.
{click.relative(sell_slot / 800.0, rebuy_coord_y / 450.0, 1.0, 0.5)}
;
; We now go back to the combine loop to do it all again
goto(combine_loop)

no_sell:
waitframe()
; Instead of selling this one, we put it in the third combine slot
{click.relative(combine_slot_3 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; We now buy back the second gift we sold, which ended up in rebuy_slot_1
{click.relative(rebuy_slot_1 / 800.0, rebuy_coord_y / 450.0, 1.0, 0.5)}
;
; Rebuying puts it back inside our inventory, so we must grab it back
click(inv_slot)
; And put it in our second combine slot
{click.relative(combine_slot_2 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; And lastly, we buy the gift in rebuy_slot_1
{click.relative(rebuy_slot_1 / 800.0, rebuy_coord_y / 450.0, 1.0, 0.5)}
click(inv_slot)
; And put it in the first combine slot
{click.relative(combine_slot_1 / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}

;
; To end, we click the combine button 
{click.relative(combine_button / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
;
; Take the combine output from the combe slot
{click.relative(combine_slot_result / 800.0, combine_coord_y / 450.0, 1.0, 0.5)}
; and put it in our inventory slot
click(inv_slot)

; We end by clicking the unwrap all button
{click.relative(735.0 / 800.0, 140.0 / 450.0, 1.0, 0.5)}
; and increment the inventory

inventory += 1.0

end:
:local string gift_type
gift_type = if(\
  target_gift == 2.0,\
  "blue",\
  if(target_gift == 1.0, "green", "red")\
) . " gift"

; add our data back in our combiner
{combiner.set(sub(d2s(10.0 + target_gift), 1, 1) . sub(d2s(100.0 + inventory), 1, 2) . "</size>"\
  . if(\
    contains({combiner.get}, "WARNING") || (impulse() == "wakeup" && budget() < 3600),\
    "<color=#FC0>WARNING=INSUFFICIENT BUDGET!"\
    . "<br>UI UPDATED WILL NOT BE AS RESPONSIVE!"\
    . "<br></color>",\
    ""\
  ) . "script will buy " . gift_type . "<br>"\
  . if(\
    impulse() == "key.{inv_fill}" && inventory <= 47.0,\
    "combining with <color=#0000>" . gift_type . "</color>",\
    "<mark=#00000050>" .\
      "<color=" . target_color . ">{start}/{inv_fill}</color> to start" .\
      "<br><color=" . target_color . ">{inv_fill}</color> fills the inventory with gifts" .\
      "<br><color=" . target_color . ">{gift_up}/{gift_down}</color> changes the target gift" .\
      "<br><color=" . target_color . ">{up}/{down}/{left}/{right}</color> moves " .\
      "<color=" . target_color . ">up/down/left/right</color>" .\
    "</mark>"\
  )\
)}

inv_slot = vec(\
  inv_coord_x + inv_slot_offset * (inventory % 12.0),\
  inv_coord_y - inv_slot_offset * floor(inventory / 12.0)\
) - offset_to_corner

canvas.clear()
; add a highlight to the selection square
canvas.rect(\
  {pos.relative(x(inv_slot) / 800.0, y(inv_slot) / 450.0, 1.0, 0.5)},\
  {pos.relative(inv_border_size / 800.0, inv_border_size / 450.0, 0.0, 0.0)},\
  "#000"\
)
; make the selection square
canvas.rect(\
  {pos.relative((x(inv_slot) + 1.0) / 800.0, (y(inv_slot) + 1.0) / 450.0, 1.0, 0.5)},\
  {pos.relative(inv_slot_size / 800.0, inv_slot_size / 450.0, 0.0, 0.0)},\
  target_color . "5"\
)
; concatenating a 5 onto the target color to make the selection square a little bit transparent
waitframe()
gotoif(start, contains({combiner.get}, "combining"))