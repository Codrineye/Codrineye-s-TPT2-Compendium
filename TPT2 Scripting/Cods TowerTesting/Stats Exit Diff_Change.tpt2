:import TowerTesting_lib
:name {package(Stats Exit Diff_Change)}

:global double Curent_Diff
:local int increment

key.t()
key.e()

; helper condition so the user can easily chose which list to use
concat("Easy -> Impossible", {difficulty_list}) != \
concat("Impossible -> Easy", {inversed_difficulty_list})

; if called by Initialiser, go to loop
; if called by an impulse, check if in towertesting
; if in towertesting, if impulse key.t, goto stats, else goto exit
; if not in towertesting quit the call
; if not called by a key impulse, we're called by winAI4 to stop towertesting at 60 ingame seconds
goto(if(\
  contains(impulse(), "{package()}"), \
  loop, \
  if(\
    contains(impulse(), "key."), \
    if(\
      isTowerTesting(), \
      if(contains(impulse(), "key.t"), stats, exit), \
      99\
    ), \
    wait_for_exit\
  )\
))
wait_for_exit:
waitwhile(global.double.get("t") % 60.0 > 0.5 || global.double.get("t") < 0.5)
exit:
exit()

stats:
wait(global.double.get("screen_delay"))
{click.relative(260.0/800.0, 275.0/450.0, 0.5, 0.5)}
goto(99)

; most important loop, it determines which difficulty is sellected
loop:
Curent_Diff = s2d(sub({difficulty_list}, increment * difficulty_spacing, difficulty_spacing), 290.0)

; we increment because we've changed difficulty
increment = (increment + 1) % difficulty_count

; prevent unwanted actions by waiting for set delay
; wait to get a signal from either Movement Manager or Initialiser
wait(global.double.get("screen_delay"))
waituntil({change_difficulty_get} || {state_get} == false)
; signal to Movement Manager that it may continue running
{change_difficulty_set(false)}
; loop if we got a signal from Movement Manager
gotoif(loop, {state_get})
